buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

version = "$mc_version-$mod_version"
group = "net.mcft.copy.aho"
archivesBaseName = "AdvHealthOptions"

minecraft {
    version = "$mc_version-$forge_version"

    replaceIn "AdvHealthOptions.java"
    replace "@VERSION@", project.version
}

repositories {
    maven {
        name "RX14"
        url "http://mvn.rx14.co.uk/repo/"
    }
}

dependencies {
    compile name: "copycore", group: "net.mcft.copy.copycore", version: "$mc_version-$copycore_version", classifier: "deobf"
}

sourceSets.main {
    java { srcDirs 'src' }
    resources { srcDirs 'resources' }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives sourcesJar
    archives deobfJar
}
